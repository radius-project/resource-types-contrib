@description('Information about what resource is calling this Recipe. Generated by Radius. For more information visit https://docs.radapp.io/reference/context-schema/ ')
param context object

extension kubernetes with {
  kubeConfig: ''
  namespace: context.runtime.kubernetes.namespace
} as kubernetes

@description('Name of the MySQL database. Defaults to the application name.')
param database string = context.resource.properties.?database ?? '${context.application.name}'

@description('MySQL username. Defaults to <application-name>-user')
param username string = context.resource.properties.?username ?? '${context.application.name}-user'

@description('The major MySQL server version in the X.Y format. Defaults to the version 8.4 if not provided.')
@allowed([
  '5.7'
  '8.0'
  '8.4'
])
param version string = context.resource.properties.?version ?? '8.4'

@description('Unique name for the MySQL deployment and service.')
var uniqueName = 'mysql-${uniqueString(context.resource.id)}'

@description('The MySQL container image to use.')
var mySqlImage = 'mysql:${version}'

@description('The port the MySQL server listens on.')
var port = 3306

@description('MySQL user password. Defaults to a unique generated value.')
var password string = uniqueString(context.resource.id, guid(uniqueName, username))

@description('MySQL server root password.')
var root_password string = uniqueString(context.resource.id, guid(uniqueName, 'root'))

resource mySql 'apps/Deployment@v1' = {
  metadata: {
    name: uniqueName
  }
  spec: {
    selector: {
      matchLabels: {
        app: context.application.name
        resource: context.resource.name
      }
    }
    template: {
      metadata: {
        labels: {
          app: context.application.name
          resource: context.resource.name
          // 'radapp.io/application': context.application == null ? '' : context.application.name
        }
      }
      spec: {
        containers: [
          {
            name: 'mysql'
            image: mySqlImage
            ports: [
              {
                containerPort: port
              }
            ]
            env: [
              {
                name: 'MYSQL_ROOT_PASSWORD'
                value: root_password
              }
              {
                name: 'MYSQL_USER'
                value: username
              }
              {
                name: 'MYSQL_PASSWORD'
                value: password
              }
              {
                name: 'MYSQL_DATABASE'
                value: database
              }
            ]
          }
        ]
      }
    }
  }
}

resource svc 'core/Service@v1' = {
  metadata: {
    name: uniqueName
    labels: {
      app: context.application.name
    }
  }
  spec: {
    type: 'ClusterIP'
    selector: {
      app: context.application.name
      resource: context.resource.name
    }
    ports: [
      {
        port: port
      }
    ]
  }
}

output result object = {
  resources: [
    '/planes/kubernetes/local/namespaces/${svc.metadata.namespace}/providers/core/Service/${svc.metadata.name}'
    '/planes/kubernetes/local/namespaces/${mySql.metadata.namespace}/providers/apps/Deployment/${mySql.metadata.name}'
  ]
  values: {
    host: '${svc.metadata.name}.${svc.metadata.namespace}.svc.cluster.local'
    port: port
    database: database
    username: username
  }
  secrets: {
    #disable-next-line outputs-should-not-contain-secrets
    password: password
  }
}
