@description('Information about what resource is calling this Recipe. Generated by Radius.')
param context object

@description('Name of the Neo4j database. Defaults to the name of the Radius resource.')
param database string = context.resource.name

@description('Neo4j username')
param user string = 'neo4j'

@description('Neo4j password')
@secure()
#disable-next-line secure-parameter-default
param password string = uniqueString(context.resource.id)

@description('Tag to pull for the neo4j container image.')
param tag string = 'community'

extension kubernetes with {
  kubeConfig: ''
  namespace: context.runtime.kubernetes.namespace
} as kubernetes

var uniqueName = 'neo4j-${uniqueString(context.resource.id)}'
var port = 7687

resource svc 'core/Service@v1' = {
  metadata: {
    name: uniqueName
    labels: {
      name: uniqueName
    }
  }
  spec: {
    type: 'ClusterIP'
    selector: {
      app: 'neo4j'
      resource: context.resource.name
    }
    ports: [
      {
        port: port
      }
    ]
  }
}

resource neo4j 'apps/StatefulSet@v1' = {
  metadata: {
    name: uniqueName
  }
  spec: {
    serviceName: uniqueName
    replicas: 1
    selector: {
      matchLabels: {
        app: 'neo4j'
        resource: context.resource.name
      }
    }
    template: {
      metadata: {
        labels: {
          app: 'neo4j'
          resource: context.resource.name
          // Label pods with the application name so `rad run` can find the logs.
          'radapp.io/application': context.application == null ? '' : context.application.name
        }
      }
      spec: {
        containers: [
          {
            name: 'neo4j'
            image: 'neo4j:${tag}'
            ports: [
              {
                containerPort: 7474
                name: 'http'
              }
              {
                containerPort: port
                name: 'bolt'
              }
            ]
            volumeMounts: [
              {
                name: 'neo4j-data'
                mountPath: '/data'
              }
            ]
          }
        ]
      }
    }
    volumeClaimTemplates: [
      {
        metadata: {
          name: 'neo4j-data'
        }
        spec: {
          accessModes: ['ReadWriteOnce']
          resources: {
            requests: {
              storage: '10Gi'
            }
          }
        }
      }
    ]
  }
}

output result object = {
  resources: [
    '/planes/kubernetes/local/namespaces/${svc.metadata.namespace}/providers/core/Service/${svc.metadata.name}'
    '/planes/kubernetes/local/namespaces/${neo4j.metadata.namespace}/providers/apps/Deployment/${neo4j.metadata.name}'
  ]
  values: {
    host: '${svc.metadata.name}.${svc.metadata.namespace}.svc.cluster.local'
    port: port
    database: database
    username: user
  }
  secrets: {
    #disable-next-line outputs-should-not-contain-secrets
    password: password
  }
}
