extension kubernetes with {
  kubeConfig: ''
  namespace: context.runtime.kubernetes.namespace
} as kubernetes


//////////////////////////////////////////
// Common Radius variables
//////////////////////////////////////////

@description('Information about what resource is calling this Recipe. Generated by Radius.')
param context object

//////////////////////////////////////////
// PostgreSQL variables
//////////////////////////////////////////

var resourceName = context.resource.name
var namespace = context.runtime.kubernetes.namespace 
var radiusSecretName = context.resource.properties.secretName
var database string = 'postgres_db'
var tag string = '16-alpine'
var port = 5432
var memory ={
  S: {
    memoryRequest: '512Mi'
  } 
  M: {
    memoryRequest: '1Gi'
  }
  L: {
    memoryRequest: '2Gi'
  }
} 

//////////////////////////////////////////
// PostgreSQL Deployment
//////////////////////////////////////////


resource postgresql 'apps/Deployment@v1' = {
  metadata: {
    name: resourceName
    namespace: namespace
  }
  spec: {
    selector: {
      matchLabels: {
        app: 'postgresql'
      }
    }
    template: {
      metadata: {
        labels: {
          app: 'postgresql'
        }
      }
      spec: {
        containers: [
          {
            // This container is the running postgresql instance.
            name: 'postgres'
            image: 'postgres:${tag}'
            ports: [
              {
                containerPort: port 
              }
            ]
            resources: {
              requests: {
                memory: memory[context.resource.properties.size].memoryRequest
              }
            }
            env: [
              {
                name: 'POSTGRES_USER'
                valueFrom: {
                  secretKeyRef: {
                    name: radiusSecretName
                    key: 'username'
                  }
                }
              }
              {
                name: 'POSTGRES_PASSWORD'
                valueFrom: {
                  secretKeyRef: {
                    name: radiusSecretName
                    key: 'password'
                  }
                }
              }
              {
                name: 'POSTGRES_DB'
                value: database
              }
            ]
          }
        ]
      }
    }
  }
}

resource svc 'core/Service@v1' = {
  metadata: {
    name: resourceName
    namespace: namespace
  }
  spec: {
    type: 'ClusterIP'
    selector: {
      app: 'postgresql'
    }
    ports: [
      {
        port: port 
      }
    ]
  }
}

//////////////////////////////////////////
// Output Radius result 
//////////////////////////////////////////

output result object = {
  resources: [
    '/planes/kubernetes/local/namespaces/${svc.metadata.namespace}/providers/core/Service/${svc.metadata.name}'
    '/planes/kubernetes/local/namespaces/${postgresql.metadata.namespace}/providers/apps/Deployment/${postgresql.metadata.name}'
  ]
  values: {
    host: '${svc.metadata.name}.${svc.metadata.namespace}.svc.cluster.local'
    port: port
    database: database
  }
}
