@description('Information about what resource is calling this Recipe. Generated by Radius. For more information visit https://docs.radapp.dev/operations/custom-recipes/')
param context object

@description('CSI driver for the persistent volume')
param csiDriver string = 'csi.example.com'

@description('CSI volume handle for the persistent volume')
param csiVolumeHandle string = 'default-volume'

@description('Storage Class for the persistent volume')
param storageClass string = ''

extension kubernetes with {
  namespace: context.runtime.kubernetes.namespace
  kubeConfig: ''

} as kubernetes

resource persistentVolume 'core/PersistentVolume@v1' = {
  metadata: {
   name: context.resource.name
    labels: {
      resource: context.resource.name
      // Label pods with the application name so `rad run` can find the logs.
      'radapp.io/application': context.application == null ? '' : context.application.name
    }
  }
  spec: union({
    // storageClassName: storageClass
    capacity: {
      storage: context.resource.properties.sizeInGib
    }
    csi: {
      driver: csiDriver
      volumeHandle: csiVolumeHandle
    }
  }, contains(context.resource.properties, 'allowedAccessModes') ? {
    accessModes: [
      context.resource.properties.allowedAccessModes
    ]
  } : {
    accessModes: [
      'ReadWriteOnce'
      'ReadOnlyMany' 
      'ReadWriteMany'
    ]
  })
}

output result object = {
  // This workaround is needed because the deployment engine omits Kubernetes resources from its output.
  // This allows Kubernetes resources to be cleaned up when the resource is deleted.
  // Once this gap is addressed, users won't need to do this.
  resources: [
    '/planes/kubernetes/local/namespaces/${context.runtime.kubernetes.namespace}/providers/core/PersistentVolume/${persistentVolume.metadata.name}'
  ]
}
